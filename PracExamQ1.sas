options ls=78;
data drivers;
infile "X:\STAT 505\skill.dat";
input sex $ t1 t2 t3;
run;
data drivers2;
set drivers;
condition=1; k=t1; output;
condition=2; k=t2; output;
condition=3; k=t3; output;
drop t1 t2 t3;
run;
*Profile plots;
proc sort data=drivers2;
by sex condition;
run;
proc means data=drivers2;
by sex condition;
var k;
output out=a mean=mean;
run;
proc gplot data=a;
axis1 length=4 in;
axis2 length=6 in;
plot mean*condition=sex / vaxis=axis1 haxis=axis2;
symbol1 v=J f=special h=2 i=join color=red;
symbol2 v=K f=special h=2 i=join color=blue;
run; quit;
*Test for interaction;
proc glm data=drivers;
class sex;
model t1 t2 t3 = sex;
manova h=sex m=t1-t2,t2-t3,t3-t1;
run; quit;
*Test for overall effect;
proc glm data=drivers;
class sex;
model t1 t2 t3=sex;
manova h=sex m=t1+t2+t3;
run; quit;
*Contrast;
data drivers3;
set drivers;
y1 = t2-t1;
y2 = t3-t2;
drop t1 t2 t3;
run;
proc iml;
  start hotel;
    mu0={0, 0};
    one=j(nrow(x),1,1);
    ident=i(nrow(x));
    ybar=x`*one/nrow(x);
    s=x`*(ident-one*one`/nrow(x))*x/(nrow(x)-1.0);
    print mu0 ybar;
    print s;
    t2=nrow(x)*(ybar-mu0)`*inv(s)*(ybar-mu0);
    f=(nrow(x)-ncol(x))*t2/ncol(x)/(nrow(x)-1);
    df1=ncol(x);
    df2=nrow(x)-ncol(x);
    p=1-probf(f,df1,df2);
    print t2 f df1 df2 p;
  finish;
  use drivers3;
  read all var{y1 y2} into x;
  run hotel;
  quit;
